/-
Shadows of a set family, the local LYM and LYM theorems, and Sperner's theorem
-/
import data.finset
import data.fintype
import to_mathlib
import basic

open fintype
open finset
open nat

variables {Î± : Type*} [decidable_eq Î±] 

def example1 : finset (finset (fin 5)) :=
{ {0,1,2}, {0,1,3}, {0,2,3}, {0,2,4} } 

-- The shadow of a set family is everything we can get by removing an element from each set
section shadow
  -- Everything we get by removing one element from the set A
  def all_removals (A : finset Î±) : finset (finset Î±) := A.image (erase A)

  -- When we remove any one element from A, each set has size r-1
  lemma all_removals_size {A : finset Î±} {r : â„•} (h : A.card = r) : all_sized (all_removals A) (r-1) := 
  begin
    intros B H,
    rw [all_removals, mem_image] at H,
    rcases H with âŸ¨i, ih, BhâŸ©,
    rw [â† Bh, card_erase_of_mem ih, h], refl
  end

  lemma mem_all_removals {A : finset Î±} {B : finset Î±} : B âˆˆ all_removals A â†” âˆƒ i âˆˆ A, erase A i = B :=
  by simp only [all_removals, mem_image]

  -- If A has size r, then there are r things we get by removing one element
  lemma card_all_removals {A : finset Î±} {r : â„•} (H : card A = r) : (all_removals A).card = r :=
  begin
    rwa [all_removals, card_image_of_inj_on],
    intros i ih j _ k,
    have q: i âˆ‰ erase A j := k â–¸ not_mem_erase i A,
    rw [mem_erase, not_and] at q,
    by_contra a, apply q a ih
  end

  -- The shadow of a set family ğ’œ is all sets we can get by removing one element from any set in ğ’œ, 
  -- and the (k times) iterated shadow is all sets we can get by removing k elements from any set in ğ’œ, 
  def shadow (ğ’œ : finset (finset Î±)) : finset (finset Î±) := ğ’œ.bind all_removals

  reserve prefix `âˆ‚`:90
  notation âˆ‚ğ’œ := shadow ğ’œ

  lemma shadow_empty : shadow (âˆ… : finset (finset Î±)) = âˆ… := by rw [shadow, bind_empty]
  lemma iter_shadow_empty (k : â„•) : shadow^[k] (âˆ… : finset (finset Î±)) = âˆ… :=
  begin
    induction k with k ih, rw iterate_zero, rwa [iterate_succ, shadow_empty]
  end

  lemma shadow_monotone {ğ’œ â„¬ : finset (finset Î±)} : ğ’œ âŠ† â„¬ â†’ âˆ‚ğ’œ âŠ† âˆ‚â„¬ := bind_sub_bind_of_sub_left _

  lemma mem_shadow {ğ’œ : finset (finset Î±)} (B : finset Î±) : B âˆˆ shadow ğ’œ â†” âˆƒ A âˆˆ ğ’œ, âˆƒ i âˆˆ A, erase A i = B := 
  by simp only [shadow, all_removals, mem_bind, mem_image]

  lemma mem_shadow' {ğ’œ : finset (finset Î±)} {B : finset Î±} : B âˆˆ shadow ğ’œ â†” âˆƒ j âˆ‰ B, insert j B âˆˆ ğ’œ :=
  begin
    rw mem_shadow,
    split,
      rintro âŸ¨A, HA, i, Hi, kâŸ©,
      rw â† k,
      refine âŸ¨i, not_mem_erase i A, _âŸ©,
      rwa insert_erase Hi,
    rintro âŸ¨i, Hi, kâŸ©,
      refine âŸ¨insert i B, k, i, mem_insert_self _ _, _âŸ©,
      rw erase_insert Hi
  end

  -- Everything in the shadow is one smaller than things in the original.
  lemma shadow_layer {ğ’œ : finset (finset Î±)} {r : â„•} (a : all_sized ğ’œ r) : all_sized (âˆ‚ğ’œ) (r-1) :=
  begin
    intros A H,
    rw [shadow, mem_bind] at H,
    rcases H with âŸ¨B, _, _âŸ©,
    exact all_removals_size (a _ â€¹_â€º) _ â€¹A âˆˆ all_removals Bâ€º,
  end

  lemma sub_iff_shadow_one {ğ’œ : finset (finset Î±)} {B : finset Î±} : B âˆˆ âˆ‚ğ’œ â†” âˆƒ A âˆˆ ğ’œ, B âŠ† A âˆ§ card (A \ B) = 1 :=
  begin
    rw mem_shadow', split, 
      rintro âŸ¨i, ih, inAâŸ©,
      refine âŸ¨insert i B, inA, subset_insert _ _, _âŸ©, rw card_sdiff (subset_insert _ _), simp [card_insert_of_not_mem ih],
    rintro âŸ¨A, hA, _âŸ©,
    rw card_eq_one at a_h_h, rcases a_h_h with âŸ¨subs, j, eqâŸ©, 
    use j, refine âŸ¨_, _âŸ©, 
    intro, have: j âˆˆ finset.singleton j, rw mem_singleton, rw â† eq at this, rw mem_sdiff at this, exact this.2 a, 
    rw â† singleton_union_eq_insert, rw â† eq, rwa sdiff_union_of_subset subs, 
  end

  -- Being in the shadow means we're a subset of something in A.
  lemma sub_of_shadow {ğ’œ : finset (finset Î±)} {B : finset Î±} : B âˆˆ âˆ‚ğ’œ â†’ âˆƒ A âˆˆ ğ’œ, B âŠ† A :=
  by rw sub_iff_shadow_one; tauto

  lemma sub_iff_shadow_iter {ğ’œ : finset (finset Î±)} {B : finset Î±} (k : â„•) : 
    B âˆˆ (shadow^[k] ğ’œ) â†” âˆƒ A âˆˆ ğ’œ, B âŠ† A âˆ§ card (A \ B) = k :=
  begin
    induction k with k ih generalizing ğ’œ B,
      simp [sdiff_eq_empty_iff_subset], 
      split,
        intro p, exact âŸ¨B, p, subset.refl _, subset.refl _âŸ©,
        rintro âŸ¨A, _, qâŸ©, rw â† subset.antisymm_iff at q, rwa q, 
    simp, rw @ih (âˆ‚ğ’œ) B, clear ih,
    split, 
      rintro âŸ¨A, hA, BsubA, card_AdiffB_is_kâŸ©, rw sub_iff_shadow_one at hA, rcases hA with âŸ¨C, CinA, AsubC, card_CdiffA_is_1âŸ©,
      refine âŸ¨C, CinA, trans BsubA AsubC, _âŸ©,
      rw card_sdiff (trans BsubA AsubC), rw card_sdiff BsubA at card_AdiffB_is_k, rw card_sdiff AsubC at card_CdiffA_is_1,
      rw [â† nat.sub_add_cancel (card_le_of_subset AsubC), nat.add_sub_assoc (card_le_of_subset BsubA), card_CdiffA_is_1, card_AdiffB_is_k, add_comm],
    rintro âŸ¨A, hA, _, _âŸ©, 
    have z: A \ B â‰  âˆ…, rw â† card_pos, rw a_h_right_right, exact nat.succ_pos _,
    rw [ne, â† exists_mem_iff_ne_empty] at z, 
    rcases z with âŸ¨i, hiâŸ©,
    have: i âˆˆ A, rw mem_sdiff at hi, exact hi.1,
    have: B âŠ† erase A i, { intros t th, apply mem_erase_of_ne_of_mem _ (a_h_right_left th), intro, rw mem_sdiff at hi, rw a at th, exact hi.2 th },
    refine âŸ¨erase A i, _, â€¹_â€º, _âŸ©,
    { rw mem_shadow, refine âŸ¨A, hA, i, â€¹_â€º, rflâŸ© }, 
    rw card_sdiff â€¹B âŠ† erase A iâ€º, rw card_erase_of_mem â€¹i âˆˆ Aâ€º, rw nat.pred_sub, rw â† card_sdiff a_h_right_left, rw a_h_right_right, simp,
  end

  lemma iter_shadow_sized {ğ’œ : finset (finset Î±)} {r k : â„•} (a : all_sized ğ’œ r) : all_sized (shadow^[k] ğ’œ) (r-k) :=
  begin
    intro B, rw sub_iff_shadow_iter,
    rintro âŸ¨A, hA, subs, cardâŸ©, rw [card_sdiff â€¹B âŠ† Aâ€º, a _ hA] at card, rw â† card, rw nat.sub_sub_self, 
    rw â† a _ hA, apply card_le_of_subset â€¹B âŠ† Aâ€º
  end
end shadow

#eval example1
#eval shadow example1

-- If there is a fintype Î± which is our universe, informally write Î±^(r) for the set
-- {A : finset Î± | |A| = r}. Then if ğ’œ is a subset of Î±^(r), we get that âˆ‚ğ’œ is a subset of Î±^(r-1).
-- The local LYM inequality says ğ’œ 'takes up less' of Î±^(r) than âˆ‚ğ’œ takes up of Î±^(r-1).
-- In particular, 
-- |ğ’œ| / choose |Î±| r â‰¤ |âˆ‚ğ’œ| / choose |Î±| (r-1)
section local_lym
  -- Start by multiplying out the inequality so it's in a slightly nicer form.
  lemma multiply_out {A B n r : â„•} (hr1 : 1 â‰¤ r) (hr2 : r â‰¤ n)
    (h : A * r â‰¤ B * (n - r + 1)) : (A : â„š) / (nat.choose n r) â‰¤ B / nat.choose n (r-1) :=
  begin
    rw div_le_div_iff; norm_cast,
    apply le_of_mul_le_mul_right _ â€¹0 < râ€º,
    cases r,
      simp,
    rw nat.succ_eq_add_one at *,
    rw [â† nat.sub_add_comm hr2, nat.add_sub_add_right] at h,
    convert nat.mul_le_mul_right (choose n r) h using 1;
      {simp [mul_assoc, nat.choose_succ_right_eq], ac_refl},
    apply nat.choose_pos hr2,
    apply nat.choose_pos (le_trans (nat.pred_le _) hr2)
  end

  -- We'll prove local LYM by a double counting argument. Here's the first set we'll count,
  -- which is effectively {(A,B) | A âˆˆ ğ’œ, B âˆˆ all_removals A}
  def the_pairs (ğ’œ : finset (finset Î±)) : finset (finset Î± Ã— finset Î±) :=
  ğ’œ.bind (Î» A, (all_removals A).image (prod.mk A))

  -- and here's how big it is: for each A âˆˆ ğ’œ there are r possible B, giving the exact cardinality.
  lemma card_the_pairs {r : â„•} (ğ’œ : finset (finset Î±)) : all_sized ğ’œ r â†’ (the_pairs ğ’œ).card = ğ’œ.card * r :=
  begin
    intro, rw [the_pairs, card_bind],
    convert sum_const_nat _,
    intros x hx, rw card_image_of_inj_on, rw card_all_removals (a _ hx),
    exact (Î» _ _ _ _ k, (prod.mk.inj k).2),
    simp only [disjoint_left, mem_image],
    rintros _ _ _ _ k a âŸ¨_, _, rflâŸ© âŸ¨_, _, aâ‚‚âŸ©,
    exact k (prod.mk.inj aâ‚‚.symm).1
  end

  -- Here's the second set we'll count. We're trying to get the same set, but we count B first, so we 
  -- overestimate a bit. It's pretty much
  -- {(A,B) | B âˆˆ âˆ‚ğ’œ, âˆƒ i âˆ‰ B: A = B âˆª i}
  def from_below [fintype Î±] (ğ’œ : finset (finset Î±)) : finset (finset Î± Ã— finset Î±) :=
  (âˆ‚ğ’œ).bind (Î» B, (univ \ B).image (Î» x, (insert x B, B)))

  -- Note the first is a subset of the second: if A âˆˆ ğ’œ and B âˆˆ all_removals A then
  -- certainly B âˆˆ âˆ‚ğ’œ, and there's some i that was removed from A to make B.
  lemma above_sub_below [fintype Î±] (ğ’œ : finset (finset Î±)) : the_pairs ğ’œ âŠ† from_below ğ’œ :=
  begin
    rintros âŸ¨A,BâŸ©,
    simp only [the_pairs, mem_all_removals, from_below, mem_shadow, true_and, and_imp, mem_bind, exists_prop, mem_sdiff, mem_image, prod.mk.inj_iff, exists_imp_distrib, mem_univ], 
    rintros A Ah B i ih z rfl rfl, 
    exact âŸ¨B, âŸ¨A, Ah, i, ih, zâŸ©, i, z â–¸ not_mem_erase _ _, z â–¸ insert_erase ih, rflâŸ©
  end

  -- We can also find how big the second set is: for each B there are (|Î±| - r + 1) choices for what to put into it.
  lemma card_from_below [fintype Î±] {ğ’œ : finset (finset Î±)} {r : â„•} (a : all_sized ğ’œ r) : (from_below ğ’œ).card = (âˆ‚ğ’œ).card * (card Î± - (r - 1)) :=
  begin
    rw [from_below, card_bind], 
    apply sum_const_nat, intros,
    rw [card_image_of_inj_on, card_univ_diff, shadow_layer a _ H], 
    intros x1 x1h _ _ h, injection h,
    have q := mem_insert_self x1 x, rw [h_1, mem_insert] at q, apply q.resolve_right (mem_sdiff.1 x1h).2,
    intros _ _ _ _ t, rw disjoint_left, simp, intros, cc
  end

  -- Putting the inequalities together gives local LYM.
  theorem local_lym [fintype Î±] {ğ’œ : finset (finset Î±)} {r : â„•} (hr1 : 1 â‰¤ r) (H : all_sized ğ’œ r):
    (ğ’œ.card : â„š) / nat.choose (card Î±) r â‰¤ (âˆ‚ğ’œ).card / nat.choose (card Î±) (r-1) :=
  begin
    cases lt_or_le (card Î±) r with z hr2,
      rw [choose_eq_zero_of_lt z, cast_zero, div_zero],  
      apply div_nonneg_of_nonneg_of_nonneg; norm_cast, any_goals {apply nat.zero_le}, 
    apply multiply_out hr1 hr2,
    rw [â† card_the_pairs _ H, â† nat.sub_sub_assoc hr2 hr1, â† card_from_below H], 
    apply card_le_of_subset, apply above_sub_below
  end
end local_lym

-- The rth slice of a set family is all of its elements which have size r.
section slice
  def slice (ğ’œ : finset (finset Î±)) (r : â„•) : finset (finset Î±) := ğ’œ.filter (Î» i, card i = r)

  reserve infix `#`:100
  notation ğ’œ#r := slice ğ’œ r

  lemma mem_slice {ğ’œ : finset (finset Î±)} {r : â„•} {A : finset Î±} : A âˆˆ ğ’œ#r â†” A âˆˆ ğ’œ âˆ§ A.card = r :=
  by rw [slice, mem_filter]

  lemma sized_slice {ğ’œ : finset (finset Î±)} {r : â„•} : all_sized (ğ’œ#r) r := Î» _ h, (mem_slice.1 h).2

  lemma ne_of_diff_slice {ğ’œ : finset (finset Î±)} {râ‚ râ‚‚ : â„•} {Aâ‚ Aâ‚‚ : finset Î±} (hâ‚ : Aâ‚ âˆˆ ğ’œ#râ‚) (hâ‚‚ : Aâ‚‚ âˆˆ ğ’œ#râ‚‚) : râ‚ â‰  râ‚‚ â†’ Aâ‚ â‰  Aâ‚‚ :=
  mt (Î» h, (sized_slice Aâ‚ hâ‚).symm.trans ((congr_arg card h).trans (sized_slice Aâ‚‚ hâ‚‚)))
end slice

-- The LYM inequality says âˆ‘_i |A#i|/(n choose i) â‰¤ 1 for an antichain A.
-- Observe that A#i is all the stuff in A which has size i, and the collection of subsets of (fin n) with size i has size (n choose i).
-- So, |A#i|/(n choose i) represents how much of each A can take up.
section lym
  -- Other proofs of LYM exist, but we'll do it by applying local LYM. 

  -- The kth decomposition of ğ’œ is defined inductively, from the top down.
  def decompose' [fintype Î±] (ğ’œ : finset (finset Î±)) : Î  (k : â„•), finset (finset Î±)
    | 0 := ğ’œ#(card Î±)
    | (k+1) := ğ’œ#(card Î± - (k+1)) âˆª shadow (decompose' k)

  -- Everything in the kth decomposition has size n-k
  lemma decompose'_layer [fintype Î±] (ğ’œ : finset (finset Î±)) (k : â„•) : all_sized (decompose' ğ’œ k) (card Î± - k) :=
  begin
    induction k with k ih; rw decompose',
      apply sized_slice,
    rw â† union_layer,
    split,
      apply sized_slice,
    apply shadow_layer ih,
  end
  
  -- Here's the first key proposition, helping to give the disjointness property in the next lemma.
  theorem antichain_prop [fintype Î±] {ğ’œ : finset (finset Î±)} {r k : â„•} (hk : k â‰¤ card Î±) (hr : r < k) (H : antichain ğ’œ) :
  âˆ€ A âˆˆ ğ’œ#(card Î± - k), âˆ€ B âˆˆ âˆ‚decompose' ğ’œ r, Â¬(A âŠ† B) :=
  begin
    intros A HA B HB k,
    rcases sub_of_shadow HB with âŸ¨C, HC, _âŸ©,
    replace k := trans k â€¹B âŠ† Câ€º, clear HB h_h B,
    induction r with r ih generalizing A C;
    rw decompose' at HC,
    any_goals { rw mem_union at HC, cases HC },
    any_goals { refine H A (mem_slice.1 HA).1 C (mem_slice.1 HC).1 _ â€¹A âŠ† Câ€º,
                apply ne_of_diff_slice HA HC _,
                apply ne_of_lt },
    { apply nat.sub_lt_of_pos_le _ _ hr hk },
    { mono },
    obtain âŸ¨_, HB', HB''âŸ© := sub_of_shadow HC,
    exact ih (lt_of_succ_lt hr) _ _ HA HB' (trans k_1 HB'')
  end

  -- This tells us that the kth decomposition is disjoint from the n-(k+1) -sized elements of ğ’œ
  lemma disjoint_of_antichain [fintype Î±] {ğ’œ : finset (finset Î±)} {k : â„•} (hk : k + 1 â‰¤ card Î±) (H : antichain ğ’œ) : 
    disjoint (ğ’œ#(card Î± - (k + 1))) (âˆ‚decompose' ğ’œ k) := 
  disjoint_left.2 $ Î» A HA HB, antichain_prop hk (lt_add_one k) H A HA A HB (subset.refl _)

  -- In particular, we can use induction and local LYM to get a bound on any top part of the sum in LYM.
  lemma card_decompose'_other [fintype Î±] {ğ’œ : finset (finset Î±)} {k : â„•} (hk : k â‰¤ card Î±) (H : antichain ğ’œ) : 
    sum (range (k+1)) (Î» r, ((ğ’œ#(card Î± - r)).card : â„š) / nat.choose (card Î±) (card Î± - r)) â‰¤ ((decompose' ğ’œ k).card : â„š) / nat.choose (card Î±) (card Î± - k) :=
  begin
    induction k with k ih,
      simp [decompose'], 
    rw [sum_range_succ, decompose'],
    convert add_le_add_left (trans (ih (le_of_lt hk)) _) _,
    { rw [card_disjoint_union, â† add_div, cast_add], exact disjoint_of_antichain hk H }, 
    { exact local_lym (nat.le_sub_left_of_add_le hk) (decompose'_layer _ _) }
  end

  -- And continuing this induction to the whole sum, we can bound the entire sum.
  lemma card_decompose_other [fintype Î±] {ğ’œ : finset (finset Î±)} (H : antichain ğ’œ) : 
    (range (card Î± + 1)).sum (Î» r, ((ğ’œ#r).card : â„š) / nat.choose (card Î±) r) â‰¤ (decompose' ğ’œ (card Î±)).card / nat.choose (card Î±) 0 :=
  begin
    rw [â† nat.sub_self (card Î±)],
    convert â† card_decompose'_other (le_refl _) H using 1,
    apply sum_flip (Î» r, ((ğ’œ#r).card : â„š) / nat.choose (card Î±) r), 
  end

  -- Finally, the bound on the RHS was just 1, so we're done.
  lemma lubell_yamamoto_meshalkin [fintype Î±] {ğ’œ : finset (finset Î±)} (H : antichain ğ’œ) : 
    (range (card Î± + 1)).sum (Î» r, ((ğ’œ#r).card : â„š) / nat.choose (card Î±) r) â‰¤ 1 :=
  begin
    transitivity,
      apply card_decompose_other H,
    rw div_le_iff; norm_cast,
      simpa only [mul_one, nat.choose_zero_right, nat.sub_self] using number_of_fixed_size (decompose'_layer ğ’œ (card Î±)),
    apply nat.choose_pos (nat.zero_le _)
  end
end lym

-- Sperner's theorem gives a bound on the size of an antichain. Again, this can be proved in a few other ways, 
-- but we'll use the machinery already developed.
-- The idea is simple: with LYM, we get a bound on how much of A can have any particular size. 
-- So to maximise the size of A, we'd like to fit it all into the term with the biggest denominator. 
-- Alternatively, âˆ‘_i |A#i|/(n choose i) â‰¤ 1, so âˆ‘_i |A#i|/(n choose (n/2)) â‰¤ 1, so âˆ‘_i |A#i| â‰¤ (n choose (n/2)), as required.
theorem sperner [fintype Î±] {ğ’œ : finset (finset Î±)} (H : antichain ğ’œ) : ğ’œ.card â‰¤ nat.choose (card Î±) (card Î± / 2) := 
begin
  have: sum (range (card Î± + 1)) (Î» (r : â„•), ((ğ’œ#r).card : â„š) / nat.choose (card Î±) (card Î±/2)) â‰¤ 1,
    transitivity,
      swap,
      exact lubell_yamamoto_meshalkin H,
    apply sum_le_sum, intros r hr,
    apply div_le_div_of_le_left; norm_cast,
    { apply nat.zero_le },
    { apply choose_pos, rw mem_range at hr, rwa â† nat.lt_succ_iff },
    { apply dominate_choose },
  rw [â† sum_div, â† sum_nat_cast, div_le_one_iff_le] at this,
    swap, norm_cast, apply choose_pos, apply nat.div_le_self, 
  norm_cast at this,
  rw â† card_bind at this,
    convert this,
    simp only [ext, mem_slice, mem_bind, exists_prop, mem_range, lt_succ_iff],
    intro a, split,
      intro ha, refine âŸ¨a.card, card_le_of_subset (subset_univ _), ha, rflâŸ©,
    rintro âŸ¨_, _, q, _âŸ©, exact q,
  intros x _ y _ ne,
  rw disjoint_left,
  intros a Ha k,
  exact ne_of_diff_slice Ha k ne rfl
end